apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

android {
  compileSdkVersion 16
  buildToolsVersion '27.0.3'

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 16
  }

  flavorDimensions "abi"

  productFlavors {
    abi_arm {
      dimension = "abi"
      ndk {
        abiFilters "armeabi-v7a"
      }

      externalNativeBuild {
        ndkBuild {
          arguments "APP_ABI=armeabi-v7a"
        }
      }
    }

    abi_x86 {
      dimension = "abi"
      ndk {
        abiFilters "x86"
      }

      externalNativeBuild {
        ndkBuild {
          arguments "APP_ABI=x86"
        }
      }
    }

    abi_all {
      dimension = "abi"
      ndk {
        abiFilters 'armeabi-v7a', 'x86'
      }
    }
  }

  externalNativeBuild {
    ndkBuild {
      path 'jni/Android.mk'
    }
  }

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      res.srcDirs = ['res']
      jni.srcDirs = ['jni']
    }
  }

  publish {
    userOrg = rootProject.ext.bintrayUserOrg
    groupId = rootProject.ext.bintrayGroupId
    artifactId = 'adblock-android'
    publishVersion = '3.1'
    licences = rootProject.ext.bintrayLicences
    desc = 'An Android library that provides the core functionality of Adblock Plus.'
    website = rootProject.ext.bintrayWebsite
    issueTracker = rootProject.ext.bintrayIssueTracker
    repository = rootProject.ext.bintrayRepository
    publications = ['abi_allRelease']
  }

  def sharedV8LibFiles = System.getenv('SHARED_V8_LIB_FILENAMES')
  if (sharedV8LibFiles != null) {
    def sharedV8LibFilesSet = []
    def sharedV8LibFilesArray = sharedV8LibFiles.split(' ')
    sharedV8LibFilesArray.each { eachFileName ->
      sharedV8LibFilesSet.add("**/" + eachFileName)
      println "[Configuration] Excluding shared v8 library " + eachFileName + " from AAR"
    }

    packagingOptions {
      excludes = sharedV8LibFilesSet
    }
  } else {
    println "[Configuration] No shared v8 libraries excluded from AAR"
  }

}